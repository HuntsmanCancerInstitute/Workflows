# Hg38 Foundation Alignment and QC Workflow 
# 30 October 2018
# David.Nix@Hci.Utah.Edu
# Huntsman Cancer Institute

############ No need to modify anything in this document #############

# Base name to prepend on all files
name = str(config["name"])
nameBuild = str(config["name"]) + "_Hg38" 

# Threads and memory
allThreads = int(config["allThreads"])
allRam = int(round(float(config["allRam"])))

# Input
xml = config["xml"]
somaticVcf = config["vcf"]

# Resources
b37Index = config["b37Index"]
hg38Index = config["hg38Index"]
chainFile = config["chain"]

# Apps in the docker container
htsLib = "/BioApps/HTSlib_1.3"
java = "java -jar -Djava.io.tmpdir=. " + "-Xmx" + str(allRam) + "G"
useq = "/BioApps/USeq_9.2.0/Apps"
crossmap = "/BioApps/CrossMap_0.2.9/CrossMap"
vt = "/BioApps/vt_2June2015/vt"

############# Rules ##############

# Workflow trigger, final Rule, cleanup and tabix index the vcfs 
rule VcfTabix:
    input:
        "Vcfs/" + nameBuild+ "_final.vcf.gz"
    output:
        "Vcfs/" + nameBuild+ "_final.vcf.gz.tbi"
    threads:
        allThreads
    log:
        "Logs/"+ nameBuild+ "_VcfTabix.log"
    shell:
        "{java} {useq}/VCFTabix -t {htsLib} -v Vcfs/ &> {log} && "
        "rm -rf *unmap "

# Convert the Foundation XML report to vcf, this is an inprecise process so watch the log for issues
rule FoundationXml2Vcf:
    output:
        temp (name +"_NoNormFoundation.vcf")
    log:
        "Logs/"+ name + "_FoundationXml2Vcf.log"
    threads:
        allThreads
    shell:
        "{java} {useq}/FoundationXml2Vcf -x {xml} "
        "-s {name}_Foundation -f {b37Index} &> {log} && "
        "cp {name}_Foundation/*vcf {output} &>> {log} && "
        "rm -rf {name}_Foundation"

# Normalize the Foundation vcf with Vt
rule NormalizeVcf:
    input:
        name +"_NoNormFoundation.vcf"
    output:
        "Vcfs/"+ name +"_B37_Foundation.vcf"
    log:
        "Logs/"+ name + "_NormalizeVcf.log"
    threads:
        allThreads
    shell:
        "{vt} normalize -r {b37Index} {input} -o {name}_tempFoundation.vcf &> {log} && "
        "{vt} decompose_blocksub {name}_tempFoundation.vcf -o {output} &>> {log} && "
        "rm -f {name}_tempFoundation.vcf "
        
# Convert the b37 coordinates to GRCh38
rule CrossMap:
    input:
        "Vcfs/"+ name +"_B37_Foundation.vcf"
    output:
        temp (nameBuild+ "_FoundationBadEnds.vcf")
    log:
        "Logs/"+ nameBuild+ "_CrossMap.log"
    threads:
        allThreads
    shell:
        "source activate python2 && "
        "{crossmap} vcf {chainFile} {input} {hg38Index} {output} &> {log} && "
        "source deactivate "

# Looks for, fixes, and integrates end= and mc= fields that CrossMap doesn't convert
rule ConvertVcfEnds:
    input:
        nameBuild+ "_FoundationBadEnds.vcf"
    output:
        "Vcfs/"+ nameBuild+ "_Foundation.vcf.gz"
    log:
        "Logs/"+ nameBuild+ "_ConvertVcfEnds.log"
    threads:
        allThreads
    shell:
        "source activate python2 && "
        "{java} {useq}/VCF2Bed -e -v {input} -s VCF2BedTemp &> {log}; "
        "if [ -f VCF2BedTemp/{nameBuild}_FoundationBadEndsPad0bp.bed.gz ]; "
        "then {crossmap} bed {chainFile} VCF2BedTemp/{nameBuild}_FoundationBadEndsPad0bp.bed.gz "
        "VCF2BedTemp/{nameBuild}_FoundationCorrEnds.bed &>> {log} && "
        "{java} {useq}/CorrectVCFEnds -v {input} -b VCF2BedTemp/{nameBuild}_FoundationCorrEnds.bed "
        "-r Vcfs/{nameBuild}_Foundation.vcf &>> {log}; "
        "else cp -f {input} Vcfs/{nameBuild}_Foundation.vcf &>> {log};"
        "gzip Vcfs/{nameBuild}_Foundation.vcf &>> {log};"
        "fi; "
	"rm -rf VCF2BedTemp && "
        "source deactivate "
             
# Compare the Foundation vcf with the recalled vcf, this generates a merged vcf too
rule FoundationVcfComparator:
    input:
        "Vcfs/"+ nameBuild+ "_Foundation.vcf.gz"
    output:
        "Vcfs/"+ nameBuild+ "_final.vcf.gz"
    log:
        "Logs/"+ nameBuild+ "_FoundationVcfComparator.log"
    threads:
        allThreads
    shell:
        "{java} {useq}/FoundationVcfComparator -f {input} "
        "-r {somaticVcf} -m {output} -e -c &> {log}"

onerror:
    shell( "rm -f STARTED COMPLETE; touch FAILED")
