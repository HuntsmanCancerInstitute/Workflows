# Primary analysis of CellRanger fastq
# 6 May 2025
# David.Nix@Hci.Utah.Edu
# Huntsman Cancer Institute

# Threads and memory
allThreads = int(config["allThreads"])
halfThreads = int(round(allThreads/2,0)) - 1
allRam = int(round(float(config["allRam"])))

# Params
sampleNames = config["sampleNames"]
nameBuild = str(config["name"])+ "_"+ config["genomeBuild"]
indexDir = config["root"] + config["indexDir"]
cellranger = config["root"] + config["cellRangerApp"]
samtools = "/BioApps/Samtools/1.19.2/bin/samtools"
fastaIndex = indexDir + "/fasta/genome.fa"
fqsConfig = config["root"] + config["fqsConfig"]
fqsApp = config["root"] + config["fqsApp"]

rule Cleanup:
    input:
        "CellRanger/outs/possorted_genome_bam.cram.crai",
        "QC/FastqScreen/"+ nameBuild+ "_screen.txt"
    output:
        "COMPLETE"
    log:
        "Logs/"+ nameBuild+ "_Cleanup.log"
    shell:
        "zip -r CellRanger/Misc.zip CellRanger/_* &> {log}; "
        "rm -rf CellRanger/SC_RNA_COUNTER_CS CellRanger/_* &>> {log};"
        "touch {output}; "

# Look for the required resources
rule CheckFilesParams:
    output:
        temp ("FileCheckOK")
    log:
        "Logs/"+ nameBuild+ "_CheckFilesParams.log"
    shell:
        "echo -ne nameBuild:'\t' &> {log} && echo {nameBuild} &>> {log} && "
        "echo -ne sampleNames:'\t' &>> {log} && echo {sampleNames} &>> {log} && "
        "echo -ne indexDir:'\t' &>> {log} && echo {indexDir} &>> {log} && "
        "echo -ne fastq:'\t' &>> {log} && ls *q.gz &>> {log} && "
        "numFastq=$(ls *q.gz | wc -l) && "
        "if [ $numFastq -lt 2 ]; then "
        "  echo 'FAILED to find at least 2 fastq.gz files, aborting.' &>> {log}; " 
        "  exit 1; "
        "else "
        "  echo 'Correct number fastq files found' &>> {log}; "
        "fi; "
        "touch {output}"

# Run CellRanger 
rule CellRangerCount:
    input:
        "FileCheckOK"
    output:
        temp ("CellRangerCountOK")
    threads:
        allThreads
    log:
        "Logs/"+ nameBuild+ "_CellRangerCount.log",
    shell:
        "{cellranger} count --localcores {threads} --localmem {allRam} "
        "--id={nameBuild} --output-dir CellRanger --fastqs=$(pwd) "
        "--sample={sampleNames} --transcriptome={indexDir} --create-bam=true &> {log}; "
        "touch {output}" 

# Convert bam to cram
rule SamtoolsCram:
    input:
        "CellRangerCountOK"
    output:
        cram = "CellRanger/outs/possorted_genome_bam.cram",
        crai = "CellRanger/outs/possorted_genome_bam.cram.crai"
    threads:
        halfThreads
    log:
        "Logs/"+ nameBuild+ "_SamtoolsCram.log"
    shell:
        "{samtools} view -C -T {fastaIndex} --write-index -o {output.cram} -@ {threads} CellRanger/outs/possorted_genome_bam.bam &> {log}; "
        "rm -rf CellRanger/outs/possorted_genome_bam.bam* &>> {log} "

# Look for contaminants
rule FastqScreen:
    input:
        "FileCheckOK"
    output:
        "QC/FastqScreen/"+ nameBuild+ "_screen.txt"
    threads:
        halfThreads
    log:
        "Logs/"+ nameBuild+ "_FastqScreen.log"
    shell:
        "shopt -s nullglob; fq=(*_R2_*.gz); "
        "{fqsApp} --threads {threads} --subset 1000000 --outdir QC/FastqScreen "
        " --force --conf {fqsConfig} --aligner bowtie2 ${{fq[0]}} &> {log} && "
        "mv -f QC/FastqScreen/*screen.txt {output} &>> {log} && "
        "rm -f QC/FastqScreen/*screen.html"

onerror:
    shell( "rm -f STARTED COMPLETE DONE; touch FAILED")
