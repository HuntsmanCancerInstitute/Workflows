#Define the resources from the config file

## Base name to prepend on all files
name = config["name"]

## For messaging
email = config["email"]

## Bed file of regions to analyze
regionsForAnalysis = config["rA"]

## Panel of Normals
mpileup = config["mpileup"]

## Bam files
tumorBam = config["tBam"]
normalBam = config["nBam"]

## Threads and memory
allThreads = int(config["threads"])
halfThreads = int(round(allThreads/2,0))
allMemory = config["memory"]

## VCF variant filtering params
minTumorAlignmentDepth = config["mtad"]
minNormalAlignmentDepth = config["mnad"]
minTumorAF = config["mtaf"]
maxNormalAF = config["mnaf"]
minTNRatio = config["mr"]
minTNDiff = config["md"]
minZScore = config["zscore"]

## Apps
useq="/uufs/chpc.utah.edu/common/home/u0028003/BioApps/USeq/Apps"
picard = "/uufs/chpc.utah.edu/common/home/u0028003/BioApps/Picard/2.1.1/picard.jar"
strelka = "/uufs/chpc.utah.edu/common/home/u0028003/BioApps/Strelka/1.0.15/InstallDir"
gatk = "/uufs/chpc.utah.edu/common/home/u0028003/BioApps/GATK/3.7/GenomeAnalysisTK.jar"
htsLib = "/uufs/chpc.utah.edu/common/home/u0028003/BioApps/HTSlib/1.3/bin"
scalpel = "/uufs/chpc.utah.edu/common/home/u0028003/BioApps/Scalpel/0.5.3/scalpel-discovery"

## Languages
java7= "/uufs/chpc.utah.edu/common/home/u0028003/BioApps/Java/1.7.0-openjdk.x86_64/bin/java -jar -Djava.io.tmpdir=. "
java8= "/uufs/chpc.utah.edu/common/home/u0028003/BioApps/Java/1.8.0_65/bin/java -jar -Djava.io.tmpdir=. "

## References
indexFasta= "/uufs/chpc.utah.edu/common/home/u0028003/HCIAtlatl/data/Human/B37/human_g1k_v37_decoy_phiXAdaptr.fasta"
dbsnp = "/uufs/chpc.utah.edu/common/home/u0028003/HCIAtlatl/data/Human/B37/MuTect/dbsnp_132_b37.leftAligned.vcf"
cosmic = "/uufs/chpc.utah.edu/common/home/u0028003/HCIAtlatl/data/Human/B37/MuTect/b37_cosmic_v54_120711.vcf"

## File resources to check before launching
requiredFiles = [mpileup, regionsForAnalysis, tumorBam, normalBam, useq, picard, strelka, gatk, htsLib, scalpel,indexFasta, dbsnp, cosmic]

############# Rules ##############        

rule CleanUp:
    input:
        name+"_Consensus.bkz.vcf.gz",
        name+ "_MutectBams"
    shell:
        "rm -rf {name}_Strelka/ {name}_Mutect/ {name}_Scalpel/ {name}_StrelkaRegionFilter/ {name}_ScalpelRegionFilter/ {name}_VCFBackgroundScanner/ && "
        "echo [`date`] rule CleanUp: COMPLETE && echo"

# Uses ls to check if all the required resources are present 
rule CheckResources:
    output:
        temp(name+ "_CheckResources.complete")
    log:
        name+ "_CheckResources.log"
    shell:
        "ls {requiredFiles} &> {log} && touch {output} && "
        "echo [`date`] rule CheckResources: COMPLETE && echo"
        
# Calls Strelka for tumor vs non matched control normal analysis
rule Strelka:
    input:
        name+ "_CheckResources.complete"
    output:
        dir = name+ "_Strelka",
        vcf1 = name+ "_Strelka/results/all.somatic.indels.vcf",
        vcf2 = name+ "_Strelka/results/all.somatic.snvs.vcf"
    log:
        name+ "_Strelka.log"
    threads: 
        allThreads
    shell:
        "rm -rf {output.dir} && "
        "{strelka}/bin/configureStrelkaWorkflow.pl "
        "--normal={normalBam} --tumor={tumorBam} --ref={indexFasta} "
        "--config={strelka}/etc/strelka_config_bwa_relaxedIndel.ini "
        "--output-dir={output.dir} &> {log} && "
        "make -j {threads} -C {output.dir} &>> {log} && "
        "echo [`date`] rule Strelka: COMPLETE && echo "

rule StrelkaPostProc:
    input:
        dir = name+ "_Strelka",
        vcf1 = name+ "_Strelka/results/all.somatic.indels.vcf",
        vcf2 = name+ "_Strelka/results/all.somatic.snvs.vcf"
    output:
        vcf = name+"_Strelka.raw.vcf.gz",
        dir = name+"_StrelkaBams"
    log:
        name+ "_StrelkaPostProc.log"
    shell:
        "rm -f {input.dir}/results/passed* && "
        "{java7} -Xmx{allMemory} {useq}/VCFMerger -o {output.vcf} -v {input.dir}/results/ &> {log} && "
        "mkdir -p {output.dir} && "
        "cp -f {input.dir}/realigned/normal.realigned.ba* {output.dir} && "
        "cp -f {input.dir}/realigned/tumor.realigned.ba* {output.dir} && "
        "echo [`date`] rule StrelkaPostProc: COMPLETE && echo "

rule StrelkaRegionFilter:
    input:
        name+"_Strelka.raw.vcf.gz"
    output:
        dir = temp(name+ "_StrelkaRegionFilter"),
        vcf = temp(name+ "_Strelka.regionFilt.vcf.gz")
    log:
        name+ "_StrelkaRegionFilter.log"
    shell:
        "{java7} -Xmx{allMemory} {useq}/VCFRegionFilter -v {input} -b {regionsForAnalysis} "
        "-s {output.dir} &> {log} && "
        "mv -f {output.dir}/*_int.vcf.gz {output.vcf} && "
        "echo [`date`] rule StrelkaRegionFilter: COMPLETE && echo "

rule StrelkaFilter:
    input:
        name+ "_Strelka.regionFilt.vcf.gz"
    output:
        vcf = name+ "_Strelka.filt.vcf.gz",
        txt = name+ "_Strelka.txt.gz"
    log:
        name+ "_StrelkaFilter.log"
    shell:
        "{java7} -Xmx{allMemory} {useq}/StrelkaVCFParser -v {input} -t {minTumorAF} "
        "-n {maxNormalAF} -u {minTumorAlignmentDepth} -o {minNormalAlignmentDepth} "
        "-d {minTNDiff} -r {minTNRatio} &> {log} && "
        "mv -f {name}_Strelka.regionFilt_Filtered.vcf.gz {output.vcf} && "
        "mv -f {name}_Strelka.regionFilt.txt.gz {output.txt} && "
        "echo [`date`] rule StrelkaFilter: COMPLETE && echo "


# Call INDELs with Scalpel, use ABRA alignments, and grab the main/somatic.indel.vcf raw variant file, twopass not neeeded
rule Scalpel:
    input:
        name+ "_Strelka"
    output:
        dir= name+ "_Scalpel",
        vcf= name+"_Scalpel.raw.vcf.gz"
    log:
        name+ "_scalpel.log"
    threads: 
        allThreads
    shell:
        "{scalpel} --somatic --ref {indexFasta} --numprocs {threads} --maxregcov 10000000 " 
        "--normal {normalBam} --tumor {tumorBam} --bed {regionsForAnalysis} --dir {output.dir} &> {log} && "
        "cat {output.dir}/main/somatic.indel.vcf | gzip > {output.vcf} && " 
        "echo [`date`] rule Scalpel: COMPLETE && echo "

rule ScalpelRegionFilter:
    input:
        name+"_Scalpel.raw.vcf.gz"
    output:
        dir = temp(name+ "_ScalpelRegionFilter"),
        vcf = temp(name+ "_Scalpel.regionFilt.vcf.gz")
    log:
        name+ "_ScalpelRegionFilter.log"
    shell:
        "{java7} -Xmx{allMemory} {useq}/VCFRegionFilter -v {input} -b {regionsForAnalysis} "
        "-s {output.dir} &> {log} && "
        "mv -f {output.dir}/*_int.vcf.gz {output.vcf} && "
        "echo [`date`] rule ScalpelRegionFilter: COMPLETE && echo "

rule ScalpelFilter:
    input:
        name+ "_Scalpel.regionFilt.vcf.gz"
    output:
        vcf= name+"_Scalpel.filt.vcf.gz",
        txt= name+"_Scalpel.txt.gz"
    log:
        name+ "_ScalpelFilter.log"
    shell:
        "{java7} -Xmx{allMemory} {useq}/ScalpelVCFParser -v {input} -t {minTumorAF} "
        "-n {maxNormalAF} -u {minTumorAlignmentDepth} -o {minNormalAlignmentDepth} "
        "-d {minTNDiff} -r {minTNRatio} &> {log} && "
        "mv -f {name}_Scalpel.regionFilt_Filtered.vcf.gz {output.vcf} && "
        "mv -f {name}_Scalpel.regionFilt.txt.gz {output.txt} && "        
        "echo [`date`] rule ScalpelFilter: COMPLETE && echo "

# Problems with threading and outputing the modified bam, so run slow...
rule Mutect:
    input:
        name+ "_Scalpel"
    output:
        bam= temp(name+ "_MutectRealigned.bam"),
        bai= temp(name+ "_MutectRealigned.bai"),
        vcf= name+"_Mutect.raw.vcf.gz"
    log:
        name+ "_Mutect.log"
    threads:
        halfThreads
    shell:
        "{java8} -Xmx{allMemory} {gatk} -T MuTect2 -I:tumor {tumorBam} -I:normal {normalBam} -mbq 20 "
        "-R {indexFasta} --dbsnp {dbsnp} --cosmic {cosmic} -dt NONE -L {regionsForAnalysis} -bamout "
        "{output.bam} -o {name}_Mutect.raw.vcf &> {log} && "
        "gzip -f {name}_Mutect.raw.vcf &>> {log} && "
        "echo [`date`] rule Mutect: COMPLETE && echo "

rule SplitMutectBam:
    input:
        bam= name+ "_MutectRealigned.bam",
        bai= name+ "_MutectRealigned.bai"
    output:
        name+ "_MutectBams"
    log:
        name+ "_SplitMutectBam.log"
    threads: 
        halfThreads
    shell:
        "mkdir -p {output} && "
        "{java8} -Xmx{allMemory} -jar {picard} SplitSamByLibrary CREATE_INDEX=true "
        "MAX_RECORDS_IN_RAM=5000000 TMP_DIR=. VALIDATION_STRINGENCY=SILENT I={input.bam} "
        "O={output} &> {log} && "
        "echo [`date`] rule SplitMutectBam: COMPLETE "

rule MutectFilter:
    input:
        name+"_Mutect.raw.vcf.gz"
    output:
        vcf = name+"_Mutect.filt.vcf.gz",
        txt = name+"_Mutect.txt.gz"
    log:
        name+ "_MutectFilter.log"
    shell:
        "{java7} -Xmx{allMemory} {useq}/MutectVCFParser -v {input} -t {minTumorAF} "
        "-n {maxNormalAF} -u {minTumorAlignmentDepth} -o {minNormalAlignmentDepth} "
        "-d {minTNDiff} -r {minTNRatio} &> {log} && "
        "mv -f {name}_Mutect.raw_Filtered.vcf.gz {output.vcf} && "
        "mv -f {name}_Mutect.raw.txt.gz {output.txt} && "
        "echo [`date`] rule MutectFilter: COMPLETE && echo "

rule VCFConsensus:
    input:
        mu= name+"_Mutect.filt.vcf.gz",
        sc= name+"_Scalpel.filt.vcf.gz",
        st= name+"_Strelka.filt.vcf.gz"
    output:
        name+"_Consensus.all.vcf.gz"
    log:
        name+"_VCFConsensus.log"
    threads:
        halfThreads
    shell:
        "{java7} -Xmx{allMemory} {useq}/VCFConsensus -p {input.mu} -s {input.st} -o {name}_tempCon.vcf.gz &> {log} && "
        "{java7} -Xmx{allMemory} {useq}/VCFConsensus -p {name}_tempCon.vcf.gz -s {input.sc} -o {output} &>> {log} && "
        "rm -f {name}_tempCon.vcf.gz && "
        "echo [`date`] rule VCFConsensus: COMPLETE && echo "

rule VCFBackgroundScanner:
    input:
        name+"_Consensus.all.vcf.gz"
    output:
        dir = temp(name+ "_VCFBackgroundScanner"),
        vcf = name+ "_Consensus.bkz.vcf.gz"
    log:
        name+ "_VCFBackgroundScanner.log"
    shell:
        "{java7} -Xmx{allMemory} {useq}/VCFBackgroundScanner -v {input} -m {mpileup} -e -z {minZScore} -c {minNormalAlignmentDepth} "
        "-s {output.dir} &> {log} && "
        "mv -f {output.dir}/{name}_Consensus.all_BKZed.vcf.gz {output.vcf} && "
        "echo [`date`] rule VCFBackgroundScanner: COMPLETE && echo "
        
        
############# Messaging ##############
onerror:
    shell( "dir=`pwd`; mail -s \"Failed: {name} ($dir)\" {email} < {log}; sleep 4s")
onsuccess:
    shell( "dir=`pwd`; mail -s \"Complete: {name} ($dir)\" {email} < {log}; sleep 4s")
